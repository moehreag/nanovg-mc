plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT' apply false
	id 'java'
	id 'io.freefair.lombok' version '8.1.+' apply false
	id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

subprojects {
	version = project.mod_version
	group = project.maven_group

	base {
		archivesName = project.archives_base_name
	}

	apply {
		plugin 'java'
		plugin 'maven-publish'
		plugin 'io.freefair.lombok'
	}

	repositories {
		maven { url 'https://jitpack.io' }
		mavenCentral()
		mavenLocal()
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release.set(8)
	}

	java {
		// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
		// if it is present.
		// If you remove this line, sources will not be generated.
		withSourcesJar()

		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
	}

	// configure the maven publication
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
			}
		}

		// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
		repositories {
			maven {
				name = "owlMaven"
				def repository = project.version.toString().contains("beta") || project.version.toString().contains("alpha") ? "snapshots" : "releases"
				url = "https://moehreag.duckdns.org/maven/"+repository
				credentials(PasswordCredentials)
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
	}

	processResources {
		inputs.property "version", project.version

		filesMatching("fabric.mod.json") {
			expand "version": project.version
		}
	}

	sourcesJar {
		from("LICENSE") {
			rename { "${it}_${project.base.archivesName.get()}" }
		}
	}

	jar {
		from("LICENSE") {
			rename { "${it}_${project.base.archivesName.get()}" }
		}
	}
}
